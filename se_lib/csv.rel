// with se_util use apply
// replace the above with fully-qualified references to se_util:apply
// See NCDNTS-853, https://relationalai.atlassian.net/browse/NCDNTS-853

// Every meta module can be bound by this:
@inline module meta_csv_config 
    def entity_name = RelName  // entity name to use in KG
    def key = RelName  // unique column name 
    def alt_key = RelName   // alternative unique column name
    def as_is_attr = RelName
    def string_attr = RelName, Any, Any // Column names and dedicated pre-processing functions
    def int_attr = RelName
    def float_attr = RelName
    def date_attr = {RelName, String}  // Date column names with their formats
    def datetime_attr = {RelName, String}  // Datetime column names with their formats
    def attr_alias_map = {RelName, RelName}  // Rename columns map
    def null_string = String  // Special string for NULL values
end

module se_csv

    @outline
    def as_is_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, val) and
        not META:null_string(val) and
        META:as_is_attr(sym) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym

    @outline
    def parse_string_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, val_str) and
        not META:null_string(val_str) and
        se_util:apply[META:string_attr[sym], val_str](val) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym, val_str

    @outline
    def parse_int_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, int_str) and
        not META:null_string(int_str) and
        parse_int(int_str, val) and
        META:int_attr(sym) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym, int_str

    @outline
    def parse_date_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, date_str) and
        not META:null_string(date_str) and
        parse_date(date_str, df, val) and
        META:date_attr(sym, df) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym, date_str, df

    @outline
    def parse_datetime_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, datetime_str) and
        not META:null_string(datetime_str) and
        parse_datetime(datetime_str, df, val) and
        META:datetime_attr(sym, df) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym, datetime_str, df

    @outline
    def parse_float_attributes[ID, DATA, META](attr, e, val) =
        ID(e, id) and
        DATA(META:key, row, id) and
        DATA(sym, row, float_str) and
        not META:null_string(float_str) and
        parse_float(float_str, val) and
        META:float_attr(sym) and
        (META:attr_alias_map[sym] <++ sym)(attr)
        from row, id, sym, float_str

    // parse_attributes - apply definitions for columns, their types, formats, and transformations
    // from the meta module to process and load CSV data file into data module
    // Example of meta module:
      // @inline def prepare_string[s in String] = uppercase[string_trim[s]]
      //
      // module cox_meta
      //   module vehicle
      //       def entity_name = :Vehicle
      //       def key = :VEHICLE_ID
      //       def as_is_attr = {
      //           :BIDDER_NAME; :WEB_LINK;
      //       }
      //       def string_attr = {
      //           (:MAKE, prepare_string);
      //           (:MODEL, prepare_string);
      //       }
      //       def int_attr = {
      //           :BIDS;
      //       }
      //       def float_attr = {
      //           :VEHICLE_MARKET_PRICE;
      //       }
      //       def date_attr = {
      //           (:SOLD_DATE, "Y-m-d");
      //       }
      //       def datetime_attr = {
      //           (:CREATE_DATE, "y-m-dTH:M:S.sss");
      //           (:LAST_ACCESS_DATE, "y-m-dTH:M:S.sss");
      //       }
      //       def attr_alias_map = {
      //           (:MAKE, :make);
      //           (:MODEL, :model);
      //           (:VEHICLE_MARKET_PRICE, :marketPrice);
      //           (:BIDDER_NAME, :bidderName);
      //           (:BIDS, :bidCount);
      //           (:WEB_LINK, :webLink);
      //           (:SOLD_DATE, :soldDate);
      //           (:CREATE_DATE, :createDate);
      //           (:LAST_ACCESS_DATE, :lastAccessDate);
      //       }
      //   end
      // end
    @inline
    def parse_attributes[ID, DATA, META](attr, e, val) =
            as_is_attributes[ID, DATA, META](attr, e, val);
            parse_string_attributes[ID, DATA, META](attr, e, val);
            parse_int_attributes[ID, DATA, META](attr, e, val);
            parse_date_attributes[ID, DATA, META](attr, e, val);
            parse_datetime_attributes[ID, DATA, META](attr, e, val);
            parse_float_attributes[ID, DATA, META](attr, e, val)

    // @outline
    // def lookup_relation[relname, attr, ENTITY_GNF, DATA, REL_ENTITY_GNF](r, e, ue) =
    //     ENTITY_GNF(:id, e, id) and
    //     DATA(mosaic_meta:account:key, row, id) and
    //     DATA(attr, row, userId) and
    //     REL_ENTITY_GNF(:id, ue, userId) and
    //     r = relname
    //     from row, id, userId

end